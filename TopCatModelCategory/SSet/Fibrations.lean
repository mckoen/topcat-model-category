import TopCatModelCategory.SSet.AnodyneExtensions
import TopCatModelCategory.SSet.Monoidal
import TopCatModelCategory.SSet.Fiber

universe u

open CategoryTheory MonoidalCategory HomotopicalAlgebra Limits MonoidalClosed Opposite Simplicial

namespace CategoryTheory

namespace Limits

variable {C : Type*} [Category C]
  {X Y Z : C ‚•§ Type u} {f : Y ‚ü∂ X} {g : Z ‚ü∂ X}

lemma FunctorToTypes.pullback_ext {A : C} {x y : (pullback f g).obj A} :
    x = y ‚Üî (pullback.fst f g).app _ x = (pullback.fst f g).app _ y ‚àß
      (pullback.snd f g).app _ x = (pullback.snd f g).app _ y := by
  constructor
  ¬∑ rintro rfl
    tauto
  ¬∑ rintro ‚ü®_, _‚ü©
    apply (PullbackCone.IsLimit.equivPullbackObj
      ((IsPullback.of_hasPullback f g).map ((evaluation _ _ ).obj A)).isLimit).injective
    ext <;> assumption

end Limits

namespace Arrow

variable {C : Type*} [Category C]

/-abbrev LiftStruct {f g : Arrow C} (œÜ : f ‚ü∂ g) := (CommSq.mk œÜ.w).LiftStruct

lemma hasLiftingProperty_iff {A B X Y : C} (i : A ‚ü∂ B) (p : X ‚ü∂ Y) :
    HasLiftingProperty i p ‚Üî ‚àÄ (œÜ : Arrow.mk i ‚ü∂ Arrow.mk p), Nonempty (LiftStruct œÜ) := by
  constructor
  ¬∑ intro _ œÜ
    have sq : CommSq œÜ.left i p œÜ.right := (CommSq.mk œÜ.w)
    exact ‚ü®{ l := sq.lift }‚ü©
  ¬∑ intro h
    exact ‚ü®fun {f g} sq ‚Ü¶ ‚ü®h (Arrow.homMk f g sq.w)‚ü©‚ü©-/

end Arrow

namespace MonoidalClosed

variable {C : Type*} [Category C] [MonoidalCategory C]
  {A B X Y : C} [Closed A] [Closed B]

@[reassoc]
lemma whiskerRight_comp_uncurry (f : A ‚ü∂ B) (g : X ‚ü∂ (ihom B).obj Y) :
    f ‚ñ∑ X ‚â´ uncurry g = uncurry (g ‚â´ (pre f).app Y) := by
  rw [uncurry_natural_left, uncurry_pre, whisker_exchange_assoc]
  rfl

@[reassoc]
lemma curry_whiskerRight_comp (f : A ‚ü∂ B) (g : B ‚äó X ‚ü∂ Y) :
    curry (f ‚ñ∑ X ‚â´ g) = curry g ‚â´ (pre f).app Y := by
  apply uncurry_injective
  rw [uncurry_curry, ‚Üê whiskerRight_comp_uncurry, uncurry_curry]

end MonoidalClosed

end CategoryTheory

namespace SSet

variable {A B X Y : SSet.{u}}

lemma yonedaEquiv_apply {n : SimplexCategory} (f : stdSimplex.obj n ‚ü∂ X) :
    yonedaEquiv f = f.app _ (stdSimplex.objEquiv.symm (ùüô _)) := rfl

lemma eq_const_iff_range_le_ofSimplex (f : X ‚ü∂ Y) (y : Y _‚¶ã0‚¶å) :
    f = const y ‚Üî Subcomplex.range f ‚â§ Subcomplex.ofSimplex y := by
  rw [Subcomplex.le_ofSimplex_iff, ‚Üê cancel_epi (toRangeSubcomplex f),
    toRangeSubcomplex_Œπ, comp_const]

section

variable {K L : SSet.{u}} (i : A ‚ü∂ B) (f : K ‚ü∂ L) (p : X ‚ü∂ Y)

noncomputable abbrev fromPushoutProduct : pushout (i ‚ñ∑ K) (A ‚óÅ f) ‚ü∂ B ‚äó L :=
  pushout.desc (B ‚óÅ f) (i ‚ñ∑ L) (by simp only [whisker_exchange])

variable {f i} in
noncomputable def fromPushoutProductCongr {K' L' A' B' : SSet.{u}} {f' : K' ‚ü∂ L'}
    {i' : A' ‚ü∂ B'}
    (e‚ÇÅ : Arrow.mk f ‚âÖ Arrow.mk f') (e‚ÇÇ : Arrow.mk i ‚âÖ Arrow.mk i') :
    Arrow.mk (fromPushoutProduct i f) ‚âÖ Arrow.mk (fromPushoutProduct i' f') := by
  refine Arrow.isoMk
    -- generalize as `pushout.mapIso`...
    { hom := pushout.map _ _ _ _ (e‚ÇÇ.hom.right ‚äó e‚ÇÅ.hom.left) (e‚ÇÇ.hom.left ‚äó e‚ÇÅ.hom.right)
        (e‚ÇÇ.hom.left ‚äó e‚ÇÅ.hom.left) ?_ ?_
      inv := pushout.map _ _ _ _ (e‚ÇÇ.inv.right ‚äó e‚ÇÅ.inv.left) (e‚ÇÇ.inv.left ‚äó e‚ÇÅ.inv.right)
        (e‚ÇÇ.inv.left ‚äó e‚ÇÅ.inv.left) ?_ ?_
      hom_inv_id := ?_
      inv_hom_id := ?_ }
    (Arrow.rightFunc.mapIso e‚ÇÇ ‚äó Arrow.rightFunc.mapIso e‚ÇÅ) ?_
  ¬∑ have := e‚ÇÇ.hom.w
    dsimp at this
    simp only [tensorHom_def, Category.assoc, whisker_exchange, ‚Üê comp_whiskerRight_assoc, this]
  ¬∑ have := e‚ÇÅ.hom.w
    dsimp at this
    simp only [tensorHom_def, Category.assoc, whisker_exchange_assoc,
      ‚Üê MonoidalCategory.whiskerLeft_comp, this]
  ¬∑ have := e‚ÇÇ.inv.w
    dsimp at this
    simp only [tensorHom_def, Category.assoc, whisker_exchange, ‚Üê comp_whiskerRight_assoc, this]
  ¬∑ have := e‚ÇÅ.inv.w
    dsimp at this
    simp only [tensorHom_def, Category.assoc, whisker_exchange_assoc,
      ‚Üê MonoidalCategory.whiskerLeft_comp, this]
  ¬∑ simp [pushout.map_comp, ‚Üê tensor_comp]
  ¬∑ simp [pushout.map_comp, ‚Üê tensor_comp]
  ¬∑ dsimp only [Arrow.mk_left, Arrow.mk_right, Functor.id_obj, Functor.id_map, Arrow.mk_hom,
      eq_mp_eq_cast, cast_eq, _root_.id_eq, eq_mpr_eq_cast, tensorIso_hom, Functor.mapIso_hom,
      Arrow.rightFunc_map]
    ext : 1
    ¬∑ simp [tensorHom_def, whisker_exchange_assoc, ‚Üê MonoidalCategory.whiskerLeft_comp]
    ¬∑ have := e‚ÇÇ.hom.w
      dsimp at this
      simp only [tensorHom_def, pushout.inr_desc_assoc, Category.assoc, pushout.inr_desc,
        ‚Üê comp_whiskerRight_assoc, whisker_exchange, this]

noncomputable def fromPushoutProductŒπŒπIso (A : K.Subcomplex) (B : L.Subcomplex) :
    Arrow.mk (fromPushoutProduct A.Œπ B.Œπ) ‚âÖ Arrow.mk (A.unionProd B).Œπ :=
  Arrow.isoMk (Subcomplex.unionProd.isPushout A B).isoPushout.symm (Iso.refl _)

noncomputable abbrev ihomToPullback :
    (ihom B).obj X ‚ü∂ pullback ((ihom A).map p) ((pre i).app Y) :=
  pullback.lift ((pre i).app X) ((ihom B).map p) (by simp)

variable {f i p} in
@[simps]
noncomputable def arrowMkFromPushoutProductHomEquiv :
    (Arrow.mk (fromPushoutProduct i f) ‚ü∂ Arrow.mk p) ‚âÉ
      (Arrow.mk f ‚ü∂ Arrow.mk (ihomToPullback i p)) where
  toFun œÜ :=
    Arrow.homMk (curry (pushout.inl _ _ ‚â´ œÜ.left))
      (pullback.lift (curry (pushout.inr _ _ ‚â´ œÜ.left)) (curry œÜ.right) (by
        have := œÜ.w
        dsimp at this
        rw [‚Üê curry_natural_right, Category.assoc, this, pushout.inr_desc_assoc,
          ‚Üê curry_whiskerRight_comp]
        dsimp)) (by
        dsimp
        ext : 1
        ¬∑ dsimp
          simp only [Category.assoc, pullback.lift_fst,
            ‚Üê curry_whiskerRight_comp, ‚Üê curry_natural_left,
            pushout.condition_assoc]
        ¬∑ dsimp
          have := œÜ.w
          dsimp at this
          simp only [Category.assoc, pullback.lift_snd, ‚Üê curry_natural_left,
            ‚Üê curry_natural_right, this, pushout.inl_desc_assoc])
  invFun œà :=
    Arrow.homMk (pushout.desc (uncurry œà.left) (uncurry (œà.right ‚â´ pullback.fst _ _)) (by
        have := œà.w =‚â´ pullback.fst _ _
        dsimp at this
        rw [Category.assoc, Category.assoc, pullback.lift_fst] at this
        rw [‚Üê uncurry_natural_left, ‚Üê this, whiskerRight_comp_uncurry]
        dsimp))
      (uncurry (œà.right ‚â´ pullback.snd _ _)) (by
        dsimp only [Arrow.mk_left, Arrow.mk_hom]
        ext : 1
        ¬∑ have := œà.w =‚â´ pullback.snd _ _
          dsimp at this
          rw [Category.assoc, Category.assoc, pullback.lift_snd] at this
          rw [pushout.inl_desc_assoc, pushout.inl_desc_assoc,
            ‚Üê uncurry_natural_right, this, uncurry_natural_left]
        ¬∑ rw [pushout.inr_desc_assoc, pushout.inr_desc_assoc,
            ‚Üê uncurry_natural_right, Category.assoc, pullback.condition,
            whiskerRight_comp_uncurry, Category.assoc]
          dsimp)
  left_inv œÜ := by
    ext : 1
    ¬∑ dsimp only [Arrow.mk_left, Arrow.mk_right, Arrow.mk_hom,
        Arrow.homMk_left, Arrow.homMk_right]
      ext : 1
      ¬∑ rw [pushout.inl_desc, uncurry_curry]
      ¬∑ rw [pushout.inr_desc, pullback.lift_fst, uncurry_curry]
    ¬∑ dsimp
      rw [pullback.lift_snd, uncurry_curry]
  right_inv œà := by
    ext : 1
    ¬∑ dsimp only [Arrow.mk_left, Arrow.mk_right, Arrow.mk_hom,
        Arrow.homMk_left, Arrow.homMk_right]
      rw [pushout.inl_desc, curry_uncurry]
    ¬∑ dsimp only [Arrow.mk_right, Arrow.mk_left, Arrow.mk_hom,
        Arrow.homMk_left, Arrow.homMk_right]
      ext : 1
      ¬∑ rw [pullback.lift_fst, pushout.inr_desc, curry_uncurry]
      ¬∑ rw [pullback.lift_snd, curry_uncurry]

variable {f i p} in
noncomputable def fromPushoutProductLiftStructEquiv
    (œÜ : Arrow.mk (fromPushoutProduct f i) ‚ü∂ Arrow.mk p) :
    Arrow.LiftStruct œÜ ‚âÉ
      Arrow.LiftStruct (arrowMkFromPushoutProductHomEquiv œÜ) where
  toFun l :=
    { l := curry l.l
      fac_left := by
        have := pushout.inl _ _ ‚â´= l.fac_left
        dsimp at this ‚ä¢
        rw [‚Üê this, pushout.inl_desc_assoc, curry_natural_left]
      fac_right := by
        have h‚ÇÅ := pushout.inr _ _ ‚â´= l.fac_left
        have h‚ÇÇ := l.fac_right
        dsimp at h‚ÇÅ h‚ÇÇ ‚ä¢
        ext : 1
        ¬∑ simp only [Category.assoc, pullback.lift_fst, ‚Üê h‚ÇÅ, pushout.inr_desc_assoc,
            curry_whiskerRight_comp]
        ¬∑ simp only [Category.assoc, pullback.lift_snd, ‚Üê h‚ÇÇ, curry_natural_right] }
  invFun l :=
    { l := uncurry l.l
      fac_left := by
        have h‚ÇÅ := l.fac_left
        have h‚ÇÇ := l.fac_right =‚â´ pullback.fst _ _
        dsimp at h‚ÇÅ h‚ÇÇ ‚ä¢
        rw [pullback.lift_fst, Category.assoc, pullback.lift_fst] at h‚ÇÇ
        ext : 1
        ¬∑ rw [pushout.inl_desc_assoc]
          apply curry_injective
          simp only [‚Üê h‚ÇÅ, curry_natural_left, curry_uncurry]
        ¬∑ rw [pushout.inr_desc_assoc]
          apply curry_injective
          simp only [‚Üê h‚ÇÇ, curry_whiskerRight_comp, curry_uncurry]
      fac_right := by
        have := l.fac_right =‚â´ pullback.snd _ _
        dsimp at this ‚ä¢
        rw [pullback.lift_snd, Category.assoc, pullback.lift_snd] at this
        apply curry_injective
        simp only [‚Üê this, ‚Üê uncurry_natural_right, curry_uncurry] }
  left_inv l := by ext : 1; apply uncurry_curry
  right_inv l := by ext : 1; apply curry_uncurry

lemma hasLiftingProperty_iHomToPullback_iff :
    HasLiftingProperty f (ihomToPullback i p) ‚Üî
      HasLiftingProperty (fromPushoutProduct i f) p := by
  simp only [Arrow.hasLiftingProperty_iff]
  constructor
  ¬∑ intro h œÜ
    exact ‚ü®(fromPushoutProductLiftStructEquiv _).symm (h _).some‚ü©
  ¬∑ intro h œà
    obtain ‚ü®œÜ, rfl‚ü© := arrowMkFromPushoutProductHomEquiv.surjective œà
    exact ‚ü®fromPushoutProductLiftStructEquiv _ ((h œÜ).some)‚ü©

end

section

instance (i : A ‚ü∂ B) (p : X ‚ü∂ Y) [Mono i] [Fibration p] :
    Fibration (ihomToPullback i p) := by
  rw [modelCategory.fibration_iff]
  intro _ _ _ hf
  simp only [modelCategory.J, MorphismProperty.iSup_iff] at hf
  obtain ‚ü®n, ‚ü®j‚ü©‚ü© := hf
  rw [hasLiftingProperty_iHomToPullback_iff]
  apply anodyneExtensions.hasLeftLiftingProperty
  refine (anodyneExtensions.arrow_mk_iso_iff ?_).2
    (anodyneExtensions.subcomplex_unionProd_mem_of_right (Subcomplex.range i) _
    (anodyneExtensions.horn_Œπ_mem _ j))
  exact fromPushoutProductCongr (Iso.refl _)
    (Arrow.isoMk (asIso (toRangeSubcomplex i)) (Iso.refl _)) ‚â™‚â´ fromPushoutProductŒπŒπIso _ _

end

@[simps! hom_left inv_left hom_right]
noncomputable def ihomToPullbackTerminalFromArrowIso (f : A ‚ü∂ B) (Z : SSet.{u}) :
    Arrow.mk (ihomToPullback f (terminal.from Z)) ‚âÖ
      Arrow.mk ((pre f).app Z) :=
  Arrow.isoMk (Iso.refl _)
    { hom := pullback.fst _ _
      inv := pullback.lift (ùüô _) (by
        apply IsTerminal.from
        apply isLimitOfHasTerminalOfPreservesLimit) (by
          apply IsTerminal.hom_ext
          apply isLimitOfHasTerminalOfPreservesLimit)
      hom_inv_id := by
        dsimp
        ext : 1
        ¬∑ simp
        ¬∑ apply IsTerminal.hom_ext
          apply isLimitOfHasTerminalOfPreservesLimit
      inv_hom_id := by simp }

instance {Z : SSet.{u}} (f : A ‚ü∂ B) [Mono f] [IsFibrant Z] :
    Fibration ((pre f).app Z) := by
  rw [fibration_iff]
  refine ((fibrations _).arrow_mk_iso_iff (ihomToPullbackTerminalFromArrowIso f Z)).1 ?_
  rw [‚Üê fibration_iff]
  infer_instance

@[simps! hom_left inv_left hom_right]
noncomputable def ihomToPullbackInitialToArrowIso
    (A : SSet.{u}) {X Y : SSet.{u}} (p : X ‚ü∂ Y):
    Arrow.mk (ihomToPullback (initial.to A) p) ‚âÖ
      Arrow.mk ((ihom A).map p) :=
  Arrow.isoMk (Iso.refl _)
    { hom := pullback.snd _ _
      inv := pullback.lift (curry (by
        apply IsInitial.to
        apply isColimitOfHasInitialOfPreservesColimit (tensorRight _))) (ùüô _) (by
          apply uncurry_injective
          apply IsInitial.hom_ext
          apply isColimitOfHasInitialOfPreservesColimit (tensorRight _))
      hom_inv_id := by
        dsimp
        ext : 1
        ¬∑ apply uncurry_injective
          apply IsInitial.hom_ext
          apply isColimitOfHasInitialOfPreservesColimit (tensorRight _)
        ¬∑ simp
      inv_hom_id := by simp }

open MorphismProperty in
instance (A : SSet.{u}) : Mono (initial.to A) := by
  have e : Arrow.mk (initial.to A) ‚âÖ Arrow.mk (‚ä• : A.Subcomplex).Œπ :=
    Arrow.isoMk (initialIsInitial.coconePointUniqueUpToIso
        (Subcomplex.isInitialBot A)) (Iso.refl _)
        (by dsimp; apply Subsingleton.elim)
  exact ((monomorphisms _).arrow_mk_iso_iff e).2
    (monomorphisms.infer_property _)

instance (p : X ‚ü∂ Y) [Fibration p] :
    Fibration ((ihom A).map p) := by
  rw [fibration_iff]
  refine ((fibrations _).arrow_mk_iso_iff (ihomToPullbackInitialToArrowIso A p)).1 ?_
  rw [‚Üê fibration_iff]
  infer_instance

instance {A X : SSet.{u}} [IsFibrant X] : IsFibrant (A ‚ü∂[SSet] X) := by
  rw [isFibrant_iff_of_isTerminal ((ihom A).map (terminal.from X))]
  ¬∑ infer_instance
  ¬∑ apply isLimitOfHasTerminalOfPreservesLimit

section

variable {t : A ‚ü∂ X} {i : A ‚ü∂ B} {p : X ‚ü∂ Y} {b : B ‚ü∂ Y} (sq : CommSq t i p b)

include sq in
noncomputable def ihomToPullbackTgt‚ÇÄMk :
    pullback ((ihom A).map p) ((pre i).app Y) _‚¶ã0‚¶å :=
  yonedaEquiv (pullback.lift (yonedaEquiv.symm (ihom‚ÇÄEquiv.symm t))
      (yonedaEquiv.symm (ihom‚ÇÄEquiv.symm b))
        (yonedaEquiv.injective (ihom‚ÇÄEquiv.injective (by
        simp only [yonedaEquiv_symm_zero, const_comp, yonedaEquiv‚ÇÄ,
          ‚Üê ihom‚ÇÄEquiv_symm_comp, ‚Üê ihom‚ÇÄEquiv_symm_comp', sq.w]))))

noncomputable def ihomToPullbackFiber : ((ihom B).obj X).Subcomplex :=
  Subcomplex.fiber (ihomToPullback i p)
    (ihomToPullbackTgt‚ÇÄMk sq)

lemma range_le_ihomToPullbackFiber_iff {Z : SSet.{u}} (f : Z ‚ü∂ (ihom B).obj X) :
    Subcomplex.range f ‚â§ ihomToPullbackFiber sq ‚Üî
      f ‚â´ (pre i).app X = SSet.const (ihom‚ÇÄEquiv.symm t) ‚àß
      f ‚â´ (ihom B).map p = SSet.const (ihom‚ÇÄEquiv.symm b) := by
  rw [ihomToPullbackFiber, ihomToPullbackTgt‚ÇÄMk, Subcomplex.le_fiber_iff, ihomToPullback,
    pullback.hom_ext_iff, Category.assoc, Category.assoc, pullback.lift_fst,
      pullback.lift_snd, const_comp, const_comp, yonedaEquiv_apply,
      ‚Üê FunctorToTypes.comp, ‚Üê FunctorToTypes.comp,
      pullback.lift_fst, pullback.lift_snd,
      yonedaEquiv_symm_zero, yonedaEquiv_symm_zero, const_app, const_app,
      SimplexCategory.const_eq_id, op_id,
      FunctorToTypes.map_id_apply, FunctorToTypes.map_id_apply,
      eq_const_iff_range_le_ofSimplex, eq_const_iff_range_le_ofSimplex,
      eq_const_iff_range_le_ofSimplex, eq_const_iff_range_le_ofSimplex,
      Subcomplex.range_comp, Subcomplex.range_comp,
      Subcomplex.range_comp, Subcomplex.range_comp]
  simp only [Subcomplex.range_Œπ]

lemma le_ihomToPullbackFiber_iff (Z : ((ihom B).obj X).Subcomplex) :
    Z ‚â§ ihomToPullbackFiber sq ‚Üî
      Z.Œπ ‚â´ (pre i).app X = SSet.const (ihom‚ÇÄEquiv.symm t) ‚àß
      Z.Œπ ‚â´ (ihom B).map p = SSet.const (ihom‚ÇÄEquiv.symm b) := by
  rw [‚Üê range_le_ihomToPullbackFiber_iff sq]
  simp only [Subcomplex.range_Œπ]

lemma ihom‚ÇÄEquiv_symm_mem_ihomToPullbackFiber_obj_zero_iff (f : B ‚ü∂ X) :
    ihom‚ÇÄEquiv.symm f ‚àà (ihomToPullbackFiber sq).obj (op ‚¶ã0‚¶å) ‚Üî
      i ‚â´ f = t ‚àß f ‚â´ p = b:= by
  have := range_le_ihomToPullbackFiber_iff sq
    (yonedaEquiv.symm (ihom‚ÇÄEquiv.symm f))
  simp only [yonedaEquiv_symm_zero, Subcomplex.range_eq_ofSimplex, yonedaEquiv‚ÇÄ,
      Subcomplex.ofSimplex_le_iff] at this
  convert this using 2
  all_goals
  ¬∑ rw [‚Üê EmbeddingLike.apply_eq_iff_eq ihom‚ÇÄEquiv.symm,
      ‚Üê EmbeddingLike.apply_eq_iff_eq yonedaEquiv.symm]
    rfl

@[reassoc (attr := simp)]
lemma ihomToPullbackFiber_ihom_map :
    (ihomToPullbackFiber sq).Œπ ‚â´ (ihom B).map p = SSet.const (ihom‚ÇÄEquiv.symm b) :=
  ((le_ihomToPullbackFiber_iff sq _).1 (by rfl)).2

@[reassoc (attr := simp)]
lemma ihomToPullbackFiber_pre_app :
    (ihomToPullbackFiber sq).Œπ ‚â´ (pre i).app X = SSet.const (ihom‚ÇÄEquiv.symm t) :=
  ((le_ihomToPullbackFiber_iff sq _).1 (by rfl)).1

instance [Mono i] [Fibration p] :
    IsFibrant (C := SSet.{u}) (ihomToPullbackFiber sq) := by
  dsimp only [ihomToPullbackFiber]
  infer_instance

end

-- a consequence of the right lifting property with respect
-- to the horn inclusion `Œõ[2, 1] ‚ü∂ Œî[2]`: it is possible
-- to compose path above if we provide a composition below
lemma exists_path_composition_above_of_fibration
    (p : X ‚ü∂ Y) [Fibration p] (x‚ÇÄ‚ÇÅ x‚ÇÅ‚ÇÇ : Œî[1] ‚ü∂ X)
    (h : stdSimplex.map (SimplexCategory.Œ¥ 0) ‚â´ x‚ÇÄ‚ÇÅ =
      stdSimplex.map (SimplexCategory.Œ¥ 1) ‚â´ x‚ÇÅ‚ÇÇ)
    (s : Œî[2] ‚ü∂ Y)
    (hs‚ÇÄ‚ÇÅ : stdSimplex.map (SimplexCategory.Œ¥ 2) ‚â´ s = x‚ÇÄ‚ÇÅ ‚â´ p)
    (hs‚ÇÅ‚ÇÇ : stdSimplex.map (SimplexCategory.Œ¥ 0) ‚â´ s = x‚ÇÅ‚ÇÇ ‚â´ p) :
    ‚àÉ (x‚ÇÄ‚ÇÇ : Œî[1] ‚ü∂ X),
      stdSimplex.map (SimplexCategory.Œ¥ 1) ‚â´ x‚ÇÄ‚ÇÇ =
        stdSimplex.map (SimplexCategory.Œ¥ 1) ‚â´ x‚ÇÄ‚ÇÅ ‚àß
      stdSimplex.map (SimplexCategory.Œ¥ 0) ‚â´ x‚ÇÄ‚ÇÇ =
        stdSimplex.map (SimplexCategory.Œ¥ 0) ‚â´ x‚ÇÅ‚ÇÇ ‚àß
        x‚ÇÄ‚ÇÇ ‚â´ p = stdSimplex.map (SimplexCategory.Œ¥ 1) ‚â´ s := by
  obtain ‚ü®t, ht‚ÇÅ, ht‚ÇÇ‚ü© := horn‚ÇÇ‚ÇÅ.isPushout.exists_desc x‚ÇÄ‚ÇÅ x‚ÇÅ‚ÇÇ h
  have sq : CommSq t (horn 2 1).Œπ p s := ‚ü®by
    apply horn‚ÇÇ‚ÇÅ.isPushout.hom_ext
    ¬∑ simp [reassoc_of% ht‚ÇÅ, ‚Üê hs‚ÇÄ‚ÇÅ]
    ¬∑ simp [reassoc_of% ht‚ÇÇ, ‚Üê hs‚ÇÅ‚ÇÇ]‚ü©
  refine ‚ü®stdSimplex.map (SimplexCategory.Œ¥ 1) ‚â´ sq.lift, ?_, ?_, ?_‚ü©
  ¬∑ rw [‚Üê ht‚ÇÅ]
    conv_rhs => rw [‚Üê sq.fac_left]
    rw [horn.Œπ_Œπ_assoc,
      ‚Üê Functor.map_comp_assoc, ‚Üê Functor.map_comp_assoc]
    congr 2
    exact (SimplexCategory.Œ¥_comp_Œ¥ (i := 1) (j := 1) (by rfl)).symm
  ¬∑ rw [‚Üê ht‚ÇÇ]
    conv_rhs => rw [‚Üê sq.fac_left]
    rw [horn.Œπ_Œπ_assoc,
      ‚Üê Functor.map_comp_assoc, ‚Üê Functor.map_comp_assoc]
    have := SimplexCategory.Œ¥_comp_Œ¥_self (n := 0) (i := 0)
    dsimp at this
    rw [this]
  ¬∑ rw [Category.assoc, sq.fac_right]

lemma exists_path_composition_above_of_fibration'
    (p : X ‚ü∂ Y) [Fibration p] (x‚ÇÄ‚ÇÅ x‚ÇÅ‚ÇÇ : Œî[1] ‚ü∂ X) (b : Y _‚¶ã0‚¶å)
    (h : stdSimplex.map (SimplexCategory.Œ¥ 0) ‚â´ x‚ÇÄ‚ÇÅ =
      stdSimplex.map (SimplexCategory.Œ¥ 1) ‚â´ x‚ÇÅ‚ÇÇ)
    (hx : x‚ÇÄ‚ÇÅ ‚â´ p = const b) :
    ‚àÉ (x‚ÇÄ‚ÇÇ : Œî[1] ‚ü∂ X),
      stdSimplex.map (SimplexCategory.Œ¥ 1) ‚â´ x‚ÇÄ‚ÇÇ =
        stdSimplex.map (SimplexCategory.Œ¥ 1) ‚â´ x‚ÇÄ‚ÇÅ ‚àß
      stdSimplex.map (SimplexCategory.Œ¥ 0) ‚â´ x‚ÇÄ‚ÇÇ =
        stdSimplex.map (SimplexCategory.Œ¥ 0) ‚â´ x‚ÇÅ‚ÇÇ ‚àß
        x‚ÇÄ‚ÇÇ ‚â´ p = x‚ÇÅ‚ÇÇ ‚â´ p := by
  obtain ‚ü®x‚ÇÄ‚ÇÇ, eq‚ÇÅ, eq‚ÇÇ, eq‚ÇÉ‚ü© := exists_path_composition_above_of_fibration p x‚ÇÄ‚ÇÅ x‚ÇÅ‚ÇÇ h
    (stdSimplex.map (SimplexCategory.œÉ 0) ‚â´ x‚ÇÅ‚ÇÇ ‚â´ p) (by
      have := h =‚â´ p
      simp only [Category.assoc] at this
      rw [‚Üê Functor.map_comp_assoc]
      have := SimplexCategory.Œ¥_comp_œÉ_of_gt (n := 0) (i := 1) (j := 0) (by simp)
      dsimp at this
      rw [this, Functor.map_comp_assoc, ‚Üê reassoc_of% h, hx, comp_const, comp_const]) (by
      have := SimplexCategory.Œ¥_comp_œÉ_self (n := 1) (i := 0)
      dsimp at this
      rw [‚Üê Functor.map_comp_assoc, this, CategoryTheory.Functor.map_id, Category.id_comp])
  refine ‚ü®x‚ÇÄ‚ÇÇ, eq‚ÇÅ, eq‚ÇÇ, ?_‚ü©
  have := SimplexCategory.Œ¥_comp_œÉ_succ (n := 1) (i := 0)
  dsimp at this
  rw [eq‚ÇÉ, ‚Üê Functor.map_comp_assoc, this, CategoryTheory.Functor.map_id, Category.id_comp]

lemma homotopy_extension_property‚ÇÅ {E K L : SSet.{u}} (i : K ‚ü∂ L) (p : E ‚ü∂ B) [Fibration p]
    (hE : K ‚äó Œî[1] ‚ü∂ E) (f : L ‚ü∂ E) (h‚ÇÅ : i ‚â´ f = Œπ‚ÇÅ ‚â´ hE)
    (hB : L ‚äó Œî[1] ‚ü∂ B) (h‚ÇÇ : Œπ‚ÇÅ ‚â´ hB = f ‚â´ p) (h‚ÇÉ : i ‚ñ∑ _ ‚â´ hB = hE ‚â´ p)  :
    ‚àÉ œà : L ‚äó Œî[1] ‚ü∂ E, Œπ‚ÇÅ ‚â´ œà = f ‚àß i ‚ñ∑ _ ‚â´ œà = hE ‚àß œà ‚â´ p = hB := by
  have h := IsPushout.of_hasPushout i Œπ‚ÇÅ
  let l : pushout i Œπ‚ÇÅ ‚ü∂ L ‚äó Œî[1] := pushout.desc Œπ‚ÇÅ (i ‚ñ∑ _) (by simp)
  have : anodyneExtensions l := sorry
  have := anodyneExtensions.hasLeftLiftingProperty this p
  obtain ‚ü®t, ht‚ÇÅ, ht‚ÇÇ‚ü© := h.exists_desc _ _ h‚ÇÅ
  have sq : CommSq t l p hB := ‚ü®by aesop‚ü©
  refine ‚ü®sq.lift, ?_, ?_, ?_‚ü©
  ¬∑ rw [‚Üê ht‚ÇÅ]
    conv_rhs => rw [‚Üê sq.fac_left, pushout.inl_desc_assoc]
  ¬∑ rw [‚Üê ht‚ÇÇ]
    conv_rhs => rw [‚Üê sq.fac_left, pushout.inr_desc_assoc]
  ¬∑ exact sq.fac_right

end SSet
