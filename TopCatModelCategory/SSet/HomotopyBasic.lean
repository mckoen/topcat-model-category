import TopCatModelCategory.SSet.Basic
import TopCatModelCategory.SSet.Fiber
import TopCatModelCategory.SSet.StandardSimplex
import TopCatModelCategory.SSet.Subcomplex

open CategoryTheory Category Simplicial MonoidalCategory Opposite
  ChosenFiniteProducts Limits

universe u

namespace SSet

noncomputable abbrev Î¹â‚€ {X : SSet.{u}} : X âŸ¶ Î”[1] âŠ— X :=
  lift (const (standardSimplex.objâ‚€Equiv.{u}.symm 0)) (ğŸ™ X)

@[reassoc (attr := simp)]
lemma Î¹â‚€_comp {X Y : SSet.{u}} (f : X âŸ¶ Y) :
    Î¹â‚€ â‰« _ â— f = f â‰« Î¹â‚€ := rfl

noncomputable abbrev Î¹â‚ {X : SSet.{u}} : X âŸ¶ Î”[1] âŠ— X :=
  lift (const (standardSimplex.objâ‚€Equiv.{u}.symm 1)) (ğŸ™ X)

@[reassoc (attr := simp)]
lemma Î¹â‚_comp {X Y : SSet.{u}} (f : X âŸ¶ Y) :
    Î¹â‚ â‰« _ â— f = f â‰« Î¹â‚ := rfl

namespace Subcomplex

variable {X Y Z : SSet.{u}} (A : X.Subcomplex) (B : Y.Subcomplex)
  (Ï† : (A : SSet) âŸ¶ (B : SSet))

@[ext]
structure RelativeMorphism where
  map : X âŸ¶ Y
  comm : A.Î¹ â‰« map = Ï† â‰« B.Î¹ := by aesop_cat

namespace RelativeMorphism

attribute [reassoc (attr := simp)] comm

@[simps]
def ofHom (f : X âŸ¶ Y) :
    RelativeMorphism (âŠ¤ : X.Subcomplex) (âŠ¤ : Y.Subcomplex)
      ((topIso X).hom â‰« f â‰« (topIso Y).inv) where
  map := f

@[simps]
def ofSimplexâ‚€ (f : X âŸ¶ Y) (x : X _[0]) (y : Y _[0])
    (h : f.app _ x = y) :
    RelativeMorphism (.ofSimplex x) (.ofSimplex y)
      (SSet.const âŸ¨y, mem_ofSimplex_obj yâŸ©) where
  map := f
  comm := by
    rw [ofSimplex_Î¹, ofSimplex_Î¹, const_comp, comp_const, h]

variable {A B Ï†} (f g : RelativeMorphism A B Ï†)

lemma map_eq_of_mem {n : SimplexCategoryáµ’áµ–} (a : X.obj n) (ha : a âˆˆ A.obj n) :
    f.map.app n a = Ï†.app n âŸ¨a, haâŸ© :=
  congr_fun (congr_app f.comm n) âŸ¨a, haâŸ©

@[simp]
lemma map_coe {n : SimplexCategoryáµ’áµ–} (a : A.obj n) :
    f.map.app n a = Ï†.app n a := by
  apply map_eq_of_mem

lemma image_le : A.image f.map â‰¤ B := by
  rintro n _ âŸ¨a, ha, rflâŸ©
  have := f.map_coe âŸ¨a, haâŸ©
  aesop

lemma le_preimage : A â‰¤ B.preimage f.map := by
  simpa only [â† image_le_iff] using f.image_le

structure Homotopy where
  h : Î”[1] âŠ— X âŸ¶ Y
  hâ‚€ : Î¹â‚€ â‰« h = f.map := by aesop_cat
  hâ‚ : Î¹â‚ â‰« h = g.map := by aesop_cat
  rel : _ â— A.Î¹ â‰« h = snd _ _ â‰« Ï† â‰« B.Î¹ := by aesop_cat

namespace Homotopy

attribute [reassoc (attr := simp)] hâ‚€ hâ‚ rel

@[simps]
noncomputable def refl : Homotopy f f where
  h := snd _ _ â‰« f.map

end Homotopy

variable (A B Ï†)

def HomotopyClass : Type u :=
  Quot (Î± := RelativeMorphism A B Ï†) (fun f g â†¦ Nonempty (Homotopy f g))

variable {A B Ï†}

def homotopyClass (f : RelativeMorphism A B Ï†) : HomotopyClass A B Ï† := Quot.mk _ f

lemma Homotopy.eq {f g : RelativeMorphism A B Ï†} (h : Homotopy f g) :
    f.homotopyClass = g.homotopyClass :=
  Quot.sound âŸ¨hâŸ©

lemma HomotopyClass.eq_homotopyClass (x : HomotopyClass A B Ï†) :
    âˆƒ (f : RelativeMorphism A B Ï†), f.homotopyClass = x :=
  Quot.mk_surjective x

variable {C : Z.Subcomplex} {Ïˆ : (B : SSet) âŸ¶ (C : SSet)}

@[simps]
def comp (f' : RelativeMorphism B C Ïˆ) {Ï†Ïˆ : (A : SSet) âŸ¶ (C : SSet)}
    (fac : Ï† â‰« Ïˆ = Ï†Ïˆ) : RelativeMorphism A C Ï†Ïˆ where
  map := f.map â‰« f'.map

variable {f g}

@[simps]
noncomputable def Homotopy.postcomp (h : Homotopy f g)
    (f' : RelativeMorphism B C Ïˆ) {Ï†Ïˆ : (A : SSet) âŸ¶ (C : SSet)}
    (fac : Ï† â‰« Ïˆ = Ï†Ïˆ) :
    Homotopy (f.comp f' fac) (g.comp f' fac) where
  h := h.h â‰« f'.map
  rel := by simp [h.rel_assoc, â† fac]

noncomputable def Homotopy.precomp
    {f' g' : RelativeMorphism B C Ïˆ} (h : Homotopy f' g')
    (f : RelativeMorphism A B Ï†) {Ï†Ïˆ : (A : SSet) âŸ¶ (C : SSet)}
    (fac : Ï† â‰« Ïˆ = Ï†Ïˆ) :
    Homotopy (f.comp f' fac) (f.comp g' fac) where
  h := _ â— f.map â‰« h.h
  rel := by
    rw [â† fac, assoc, â† MonoidalCategory.whiskerLeft_comp_assoc,
      f.comm, MonoidalCategory.whiskerLeft_comp_assoc,
      h.rel, whiskerLeft_snd_assoc]

def HomotopyClass.postcomp (h : HomotopyClass A B Ï†)
    (f' : RelativeMorphism B C Ïˆ) {Ï†Ïˆ : (A : SSet) âŸ¶ (C : SSet)}
    (fac : Ï† â‰« Ïˆ = Ï†Ïˆ) :
    HomotopyClass A C Ï†Ïˆ :=
  Quot.lift (fun f â†¦ (f.comp f' fac).homotopyClass)
    (fun _ _ âŸ¨hâŸ© â†¦ (h.postcomp f' fac).eq) h

@[simp]
lemma homotopyClass_postcomp
    (f : RelativeMorphism A B Ï†)
    (f' : RelativeMorphism B C Ïˆ) {Ï†Ïˆ : (A : SSet) âŸ¶ (C : SSet)}
    (fac : Ï† â‰« Ïˆ = Ï†Ïˆ) :
    f.homotopyClass.postcomp f' fac =
      (f.comp f' fac).homotopyClass := rfl

end RelativeMorphism

end Subcomplex

/-section

variable (X : SSet.{u})

def Ï€â‚€ := Quot (Î± := X _[0]) (fun x y â†¦ âˆƒ (p : X _[1]), X.Î´ 1 p = x âˆ§ X.Î´ 0 p = y)

def toÏ€â‚€ (x : X _[0]) : Ï€â‚€ X := Quot.mk _ x

lemma toÏ€â‚€_surjective : Function.Surjective X.toÏ€â‚€ := by
  apply Quot.mk_surjective

lemma toÏ€â‚€_congr (p : X _[1]) : X.toÏ€â‚€ (X.Î´ 1 p) = X.toÏ€â‚€ (X.Î´ 0 p) :=
  Quot.sound âŸ¨p, rfl, rflâŸ©

end-/

namespace KanComplex

def Ï€ (n : â„•) (X : SSet.{u}) (x : X _[0]) : Type u :=
  Subcomplex.RelativeMorphism.HomotopyClass
    (subcomplexBoundary n) (Subcomplex.ofSimplex x)
      (const âŸ¨x, Subcomplex.mem_ofSimplex_obj xâŸ©)

variable {X Y : SSet.{u}} (f : X âŸ¶ Y) (n : â„•)
  (x : X _[0]) (y : Y _[0]) (h : f.app _ x = y)

def mapÏ€ (p : Ï€ n X x) : Ï€ n Y y :=
  p.postcomp (.ofSimplexâ‚€ f x y h) (by rw [comp_const])

variable {Z : SSet.{u}} (g : Y âŸ¶ Z) (z : Z _[0]) (h' : g.app _ y = z)

lemma mapÏ€_mapÏ€ (p : Ï€ n X x) :
    mapÏ€ g n y z h' (mapÏ€ f n x y h p) =
      mapÏ€ (f â‰« g) n x z (by simp [h, h']) p := by
  obtain âŸ¨h, rflâŸ© := p.eq_homotopyClass
  rfl

lemma mapÏ€_comp_mapÏ€ :
    mapÏ€ g n y z h' âˆ˜ mapÏ€ f n x y h = mapÏ€ (f â‰« g) n x z (by simp [h, h']) := by
  ext
  apply mapÏ€_mapÏ€

@[simp]
lemma mapÏ€_id (p : Ï€ n X x) :
    mapÏ€ (ğŸ™ X) n x x rfl p = p := by
  obtain âŸ¨h, rflâŸ© := p.eq_homotopyClass
  rfl

def mapÏ€EquivOfIso (e : X â‰… Y) (n : â„•) (x : X _[0]) (y : Y _[0]) (h : e.hom.app _ x = y) :
    Ï€ n X x â‰ƒ Ï€ n Y y where
  toFun := mapÏ€ e.hom n x y h
  invFun := mapÏ€ e.inv n y x (by simp [â† h])
  left_inv _ := by simp [mapÏ€_mapÏ€]
  right_inv _ := by simp [mapÏ€_mapÏ€]

end KanComplex

end SSet
